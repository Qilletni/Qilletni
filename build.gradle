plugins {
    id 'antlr'
    id 'idea'
    id 'maven-publish'
    id 'com.vanniktech.maven.publish' version '0.35.0'
}

group = 'dev.qilletni.impl'
version = '1.0.2-SNAPSHOT'

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // SLF4J API dependency
    implementation 'org.apache.logging.log4j:log4j-api:2.25.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.25.3'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.25.1'

    implementation 'org.antlr:antlr4-runtime:4.13.1'
    antlr 'org.antlr:antlr4:4.13.1'

    implementation 'com.google.code.gson:gson:2.10.1'

    implementation project(':qilletni-api')
}

mavenPublishing {
    coordinates(group, "qilletni", version)

    pom {
        name = "Qilletni"
        description = "Implementation of the Qilletni DSL"
        inceptionYear = "2024"
        url = "https://qilletni.dev/"
        licenses {
            license {
                name = "MIT"
                url = "https://mit-license.org/"
                distribution = "https://mit-license.org/"
            }
        }
        developers {
            developer {
                id = "RubbaBoy"
                name = "Adam Yarris"
                url = "https://github.com/RubbaBoy/"
            }
        }
        scm {
            url = "https://github.com/Qilletni/Qilletni"
            connection = "scm:git:git://github.com/Qilletni/Qilletni.git"
            developerConnection = "scm:git:ssh://git@github.com/Qilletni/Qilletni.git"
        }
    }
}

generateGrammarSource {
    var pkg = 'dev.qilletni.impl.antlr'
    outputDirectory = new File("${project.buildDir}/generated-src/antlr/main/${pkg.split("\\.").join("/")}".toString())
    maxHeapSize = "128m"
    arguments += ['-visitor', '-no-listener']
}

compileJava.dependsOn generateGrammarSource

sourceSets {
    generated {
        java.srcDir 'generated-src/antlr/main/'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.vanniktech.maven.publish'

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url = 'https://central.sonatype.com/repository/maven-snapshots/'
        }
    }

    java {
        withSourcesJar()
        sourceCompatibility = '22'
        targetCompatibility = '22'
    }
}

tasks.named('sourcesJar').configure {
    dependsOn 'generateGrammarSource'
}

test {
    useJUnitPlatform()
}
